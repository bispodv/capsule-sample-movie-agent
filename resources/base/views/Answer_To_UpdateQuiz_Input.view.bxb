input-view {
  match: Answer (this) {
    to-input: UpdateQuiz (action) {
      from-input: Quiz (quiz) {
        min (Required)
        max (One)
      }
    }
  }
  message {
    macro (QUIZ_UPDATE_PROMPT) {
      param (quiz) {
        expression (quiz)
      }
    }
  }

  render {
    selection-of (action.quiz.questions[action.quiz.currentQuestion].options) {
      navigation-mode {
        if ($handsFree) {
          read-many {
            page-size (5)
            list-summary {
              macro (QUIZ_UPDATE_PROMPT) {
                param (quiz) {
                  expression (quiz)
                }
              }
            }
            page-content (page) {
              underflow-statement {
                macro (HANDS_FREE_OPTION_UNDERFLOW_STATEMENT)
              }
              item-selection-question {
                if (exists(page) && isFirstNavPage(page) && isLastNavPage(page)) { // There is only one page 
                  template ("")
                }
                else-if (exists(page) && !isLastNavPage(page)) {
                  macro (HANDS_FREE_OPTION_ITEM_SELECTION_MORE_PAGES)
                }
                else { // Last page
                  macro (HANDS_FREE_OPTION_ITEM_SELECTION_LAST_PAGES)
                }
              }
              overflow-statement {
                macro (HANDS_FREE_OPTION_OVERFLOW_STATEMENT)
              }
              overflow-question {
                macro (HANDS_FREE_OPTION_OVERFLOW_QUESTION)
              }
              page-marker {
                if (exists(page) && !isFirstNavPage(page) && isLastNavPage(page)) {
                  macro (HANDS_FREE_OPTION_LAST_OPTION) {
                    param (page) {
                      expression (page)
                    }
                  }
                }
              }
            }
          }
        }
      }
      where-each (option) {
        spoken-summary {
          template ("#{value(option.alias)} ... #{value(option.text)}")
        }
        cell-card {
          slot2 {
            content {
              primary {
                template ("[#{value(option.alias)}. ]#{value(option.text)}")
              }
            }
          }
        }
      }
    }
  }
}